cmake_minimum_required(VERSION 3.10)

project(Togl)
set(PACKAGE_NAME Togl)
set(PACKAGE_VERSION 2.2)
set(PACKAGE_POSTFIX_VERSION 22)

find_package(OpenGL)
find_package(Python2)

set(TOGL_HEADERS
    image.h
    togl.h
    toglDecls.h
    toglpy.h
)

set(TOGL_COMMON_SOURCES
    togl.c
    toglProcAddr.c
    toglStubInit.c
)
if(WIN32)
    set(TOGL_WINDOWINGSYSTEM "TOGL_WGL")
    if(MSVC)
        add_definitions(-D_CRT_SECURE_NO_WARNINGS)
    endif()
    include(FetchContent)
    FetchContent_Declare(
        tcl_core
        GIT_REPOSITORY    https://github.com/tcltk/tcl
        GIT_TAG           core-8-5-branch
    )
    FetchContent_Populate(tcl_core)
    FetchContent_Declare(
        tk_core
        GIT_REPOSITORY    https://github.com/tcltk/tk
        GIT_TAG           core-8-5-branch
    )
    FetchContent_Populate(tk_core)
endif()

configure_file(togl_ws.h.in togl_ws.h @ONLY)

set(unit_build_file ${CMAKE_CURRENT_BINARY_DIR}/togl_ub.c)

file(WRITE ${unit_build_file} "/* Unity Build generated by CMake */\n")
foreach(source_file IN LISTS TOGL_COMMON_SOURCES )
    file(APPEND ${unit_build_file} "#include <${CMAKE_CURRENT_SOURCE_DIR}/${source_file}>\n")
endforeach(source_file)

add_library(Togl SHARED ${TOGL_HEADERS} ${unit_build_file})
set_target_properties(Togl PROPERTIES  OUTPUT_NAME "${PACKAGE_NAME}${PACKAGE_POSTFIX_VERSION}")
set(PKG_LIB_FILE Togl "${PACKAGE_NAME}${PACKAGE_POSTFIX_VERSION}.dll")

configure_file(pkgIndex.tcl.in pkgIndex.tcl @ONLY)

add_library(stublibrary STATIC ${TOGL_HEADERS} toglStubLib.c)
set_target_properties(stublibrary PROPERTIES  OUTPUT_NAME "${PACKAGE_NAME}stub${PACKAGE_POSTFIX_VERSION}")
add_definitions(-DUSE_TOGL_STUBS=1)
add_definitions(-DUSE_TCL_STUBS=1)
add_definitions(-DUSE_TK_STUBS=1)
add_definitions(-DBUILD_togl=1)

target_include_directories(Togl PRIVATE ${CMAKE_CURRENT_BINARY_DIR})
target_include_directories(Togl PRIVATE ${CMAKE_CURRENT_SOURCE_DIR})
target_include_directories(stublibrary PRIVATE ${CMAKE_CURRENT_BINARY_DIR})
target_include_directories(stublibrary PRIVATE ${CMAKE_CURRENT_SOURCE_DIR})
add_dependencies(Togl OpenGL::GL)

if(WIN32)
    get_filename_component(Python2_ROOT_DIR ${Python2_EXECUTABLE} DIRECTORY)
    target_link_directories(Togl PRIVATE "${Python2_ROOT_DIR}/tcl")
    target_link_libraries(Togl PRIVATE tclstub85)
    target_link_libraries(Togl PRIVATE tkstub85)
    target_link_libraries(Togl PRIVATE Opengl32)

    target_include_directories(Togl PRIVATE "${tcl_core_SOURCE_DIR}/win")
    target_include_directories(Togl PRIVATE "${tcl_core_SOURCE_DIR}/generic")
    target_include_directories(Togl PRIVATE "${tk_core_SOURCE_DIR}/win")
    target_include_directories(Togl PRIVATE "${tk_core_SOURCE_DIR}/generic")
    target_include_directories(Togl PRIVATE "${tk_core_SOURCE_DIR}/xlib")

    target_include_directories(stublibrary PRIVATE "${tcl_core_SOURCE_DIR}/win")
    target_include_directories(stublibrary PRIVATE "${tcl_core_SOURCE_DIR}/generic")
    target_include_directories(stublibrary PRIVATE "${tk_core_SOURCE_DIR}/win")
    target_include_directories(stublibrary PRIVATE "${tk_core_SOURCE_DIR}/generic")
    target_include_directories(stublibrary PRIVATE "${tk_core_SOURCE_DIR}/xlib")

    install(TARGETS Togl
        RUNTIME DESTINATION lib/${PACKAGE_NAME}${PACKAGE_VERSION}
        ARCHIVE DESTINATION lib/${PACKAGE_NAME}${PACKAGE_VERSION})
    install(FILES
        ${CMAKE_CURRENT_SOURCE_DIR}/LICENSE
        ${CMAKE_CURRENT_BINARY_DIR}/pkgIndex.tcl
        DESTINATION lib/${PACKAGE_NAME}${PACKAGE_VERSION}
        )
    install(TARGETS stublibrary
        ARCHIVE DESTINATION lib)
    install(FILES
        togl.h
        ${CMAKE_CURRENT_BINARY_DIR}/togl_ws.h
        toglDecls.h
        DESTINATION include)
endif()