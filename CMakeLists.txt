cmake_minimum_required(VERSION 3.10)

# Requires
# https://download.microsoft.com/download/7/9/6/796EF2E4-801B-4FC4-AB28-B59FBF6D907B/VCForPython27.msi

project(Togl)
set(PACKAGE_NAME Togl)
set(PACKAGE_VERSION 2.2)
set(PACKAGE_POSTFIX_VERSION 22)

find_package(OpenGL)

set(TOGL_HEADERS
    image.h
    togl.h
    toglDecls.h
    toglpy.h
)

set(TOGL_COMMON_SOURCES
    togl.c
    toglProcAddr.c
    toglStubInit.c
)
if(WIN32)
    set(TOGL_WINDOWINGSYSTEM "TOGL_WGL")
    if(MSVC)
        add_definitions(-D_CRT_SECURE_NO_WARNINGS)
    endif()
    include(FetchContent)
    FetchContent_Declare(
        python27wintcl
        GIT_REPOSITORY    https://github.com/python/cpython-bin-deps
        GIT_TAG           origin/tcltk-8.6.6.0
    )
    FetchContent_Populate(python27wintcl)
    FetchContent_Declare(
        tclsource
        GIT_REPOSITORY    https://github.com/python/cpython-source-deps/
        GIT_TAG           origin/tcl
    )
    FetchContent_Populate(tclsource)
    FetchContent_Declare(
        tksource
        GIT_REPOSITORY    https://github.com/python/cpython-source-deps/
        GIT_TAG           origin/tk
    )
    FetchContent_Populate(tksource)
endif()

configure_file(togl_ws.h.in togl_ws.h @ONLY)

set(unit_build_file ${CMAKE_CURRENT_BINARY_DIR}/togl_ub.c)

file(WRITE ${unit_build_file} "/* Unity Build generated by CMake */\n")
foreach(source_file IN LISTS TOGL_COMMON_SOURCES )
    file(APPEND ${unit_build_file} "#include <${CMAKE_CURRENT_SOURCE_DIR}/${source_file}>\n")
endforeach(source_file)

add_library(Togl SHARED ${TOGL_HEADERS} ${unit_build_file})
set_target_properties(Togl PROPERTIES  OUTPUT_NAME "${PACKAGE_NAME}${PACKAGE_POSTFIX_VERSION}")
set(PKG_LIB_FILE Togl "${PACKAGE_NAME}${PACKAGE_POSTFIX_VERSION}.dll")

configure_file(pkgIndex.tcl.in pkgIndex.tcl @ONLY)

add_library(stublibrary STATIC ${TOGL_HEADERS} toglStubLib.c)
set_target_properties(stublibrary PROPERTIES  OUTPUT_NAME "${PACKAGE_NAME}stub${PACKAGE_POSTFIX_VERSION}")
add_definitions(-DUSE_TOGL_STUBS=1)
add_definitions(-DUSE_TCL_STUBS=1)
add_definitions(-DUSE_TK_STUBS=1)
add_definitions(-DBUILD_togl=1)

target_include_directories(Togl PRIVATE ${CMAKE_CURRENT_BINARY_DIR})
target_include_directories(Togl PRIVATE ${CMAKE_CURRENT_SOURCE_DIR})
target_include_directories(stublibrary PRIVATE ${CMAKE_CURRENT_BINARY_DIR})
target_include_directories(stublibrary PRIVATE ${CMAKE_CURRENT_SOURCE_DIR})
add_dependencies(Togl OpenGL::GL)

if(WIN32)
    if( CMAKE_SIZEOF_VOID_P EQUAL 8 )
    target_include_directories(Togl PRIVATE "${python27wintcl_SOURCE_DIR}/amd64/include")
    target_link_directories(Togl PRIVATE "${python27wintcl_SOURCE_DIR}/amd64/lib")
    target_include_directories(stublibrary PRIVATE "${python27wintcl_SOURCE_DIR}/amd64/include")
    target_link_directories(stublibrary PRIVATE "${python27wintcl_SOURCE_DIR}/amd64/lib")
    else()
        target_include_directories(Togl PRIVATE "${python27wintcl_SOURCE_DIR}/win32/include")
        target_link_directories(Togl PRIVATE "${python27wintcl_SOURCE_DIR}/win32/lib")
        target_include_directories(stublibrary PRIVATE "${python27wintcl_SOURCE_DIR}/win32/include")
        target_link_directories(stublibrary PRIVATE "${python27wintcl_SOURCE_DIR}/win32/lib")
    endif()
    target_link_libraries(Togl PRIVATE tclstub86)
    target_link_libraries(Togl PRIVATE tkstub86)
    target_link_libraries(Togl PRIVATE Opengl32)
    target_include_directories(Togl PRIVATE "${tksource_SOURCE_DIR}/generic")
    target_include_directories(Togl PRIVATE "${tksource_SOURCE_DIR}/win")
    target_include_directories(Togl PRIVATE "${tclsource_SOURCE_DIR}/generic")
    target_include_directories(Togl PRIVATE "${tclsource_SOURCE_DIR}/win")
    install(TARGETS Togl
        RUNTIME DESTINATION lib/${PACKAGE_NAME}${PACKAGE_VERSION}
        ARCHIVE DESTINATION lib/${PACKAGE_NAME}${PACKAGE_VERSION})
    install(FILES
        ${CMAKE_CURRENT_SOURCE_DIR}/LICENSE
        ${CMAKE_CURRENT_BINARY_DIR}/pkgIndex.tcl
        DESTINATION lib/${PACKAGE_NAME}${PACKAGE_VERSION}
        )
    install(TARGETS stublibrary
        ARCHIVE DESTINATION lib)
    install(FILES
        togl.h
        ${CMAKE_CURRENT_BINARY_DIR}/togl_ws.h
        toglDecls.h
        DESTINATION include)
endif()